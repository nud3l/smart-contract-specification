(* Grammar of the smart contract specification language *)
Specification = Statement*;
Statement = '@', (Configuration | Storage | Global | Function);

(* Configuration *)
Configuration = 'config' ConfigurationExp*;
ConfigurationExp = ConfigurationIdentifier ' : ' ConfigurationTypes;
ConfigurationIdentifier = 'target' | 'language' | 'meter';
ConfigurationTypes = TargetType | LanguageType | MeterType;
TargetType = 'EVM' | 'EWASM' | 'IELE' | 'SCILLA';
ContractType = 'Solidity' | 'Vyper' | 'Flint';
MeterType = 'Gas';

(* Storage *)
Storage = 'storage' StorageTypedExp*;
StorageTypedExp = TermTyped | MappingTyped;
TermTyped = Identifier ' : ' BuiltinTypes;
MappingTyped = Identifier '[' StorageTypedExp '] => ' TermTyped;
StorageUntypedExp = Identifier | Mapping;
Mapping = Identifier '[' StorageUntypedExp ']';

(* Global specification *)
Global = 'global' BooleanExp*;

(* Function level specification *)
Function = FunctionDecorator FunctionExp;
FunctionDecorator = 'pure' | 'transition';
FunctionExp = FunctionSignature, (PureFunction | TransitionFunction);
FunctionSignature = Identifier '(', (Identifier*)?, ')';
PureFunction = Inputs Outputs PostCondition;
TransitionFunction = Inputs Assign PreCondition PostCondition;
Inputs = 'inputs:' StorageTypedExp*;
Outputs = 'outputs:', (StorageUntypedExp | TermTyped)*;
Assign = 'assigns:', StorageUntypedExp*;
PreCondition = 'requires:' BooleanExp*;
PostCondition = 'ensures:' BooleanExp*;

(* Expressions *))
BasicExp = StorageUntypedExp | ArithmeticExp | Initial | Literal;
BooleanExp = BasicExp Boolean BasicExp;
ArithmeticExp = BasicExp Arithmetic BasicExp;

(* Operators *)
Boolean = '==' | '<=' | '>=' | '<' | '>';
Arithmetic = '+' | '-' | '*' | '/';
Initial = 'initial(' StorageUntypedExp ')';

(* Identifiers *)
Identifier = [a-zA-Z_$] [a-zA-Z_$0-9]*;

(* Built-in Types *)
BuiltinTypes = 'Int' | 'Address' | 'Bool' | 'Asset' | 'String' | 'Bytes';

(* Literals *)
Literal = NumberLiteral | StringLiteral | HexLiteral | TrueLiteral | FalseLiteral;
NumberLiteral = HexNumber | DecimalNumber;
HexNumber = '0x' [0-9a-fA-F]+;
DecimalNumber = [0-9]+;
TrueLiteral = 'True';
FalseLiteral = 'False';
HexLiteral = 'hex' ('"' ([0-9a-fA-F]{2})* '"' | '\'' ([0-9a-fA-F]{2})* '\'');
StringLiteral = '"' ([^"\r\n\\] | '\\' .)* '"';
